package compilateurYal.analyse ;

import java.util.*;
import compilateurYal.arbre.* ;
import compilateurYal.arbre.expressions.* ;
import compilateurYal.arbre.expressions.arithmetique.*;
import compilateurYal.arbre.instructions.* ;
import compilateurYal.exceptions.AnalyseSyntaxiqueException;

import java_cup.runtime.*;

action code {:
             :} ;

parser code {:

    public void report_error(String message, Object info) {

        HashMap<Integer, String> lesTerminaux = new HashMap<>() ;
    
        lesTerminaux.put(new Integer(CodesLexicaux.DEBUT), "debut") ;
        lesTerminaux.put(new Integer(CodesLexicaux.FIN), "fin") ;
        lesTerminaux.put(new Integer(CodesLexicaux.POINTVIRGULE), ";") ;

        StringBuffer m = new StringBuffer() ;

        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

            if (s.left >= 0) {                
                m.append("\tligne : " + (s.left + 1)) ;
                if (s.right >= 0)                    
                    m.append(" colonne : " + (s.right+1)) ;
            }
            
            if (s.value != null) {
                lesTerminaux.put(CodesLexicaux.CSTENTIERE, "" + s.value) ;
            }

            if (lesTerminaux.containsKey(new Integer(s.sym))) {
                m.append(" dernier token lu : " + lesTerminaux.get(new Integer(s.sym))) ;
            }
            else {
                m.append(" expression non termin√©e") ;
            }

        }
        throw new AnalyseSyntaxiqueException("" + m) ;
    }

    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
    }
:};
   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
/* Terminals (tokens returned by the scanner).  */

terminal Symbol     PROGRAMME, DEBUT, FIN, POINTVIRGULE, ECRIRE, LIRE, ENTIER, EGAL,
                    PAR_OUV, PAR_FER, CRO_OUV, CRO_FER, PLUS, MOINS, FOIS, DIV;
terminal String     IDF, CSTENTIERE;

/* Non terminals used in the grammar section.  */

non terminal ArbreAbstrait PROG, LINST, INST, LDECL, DECL_VAR, ECR, LECTURE, AFF ;
non terminal Expression EXP, OPER;

precedence left PLUS, MOINS, FOIS, DIV;

/* ----------------------------Grammar Section-------------------- */
 
start with PROG ;

PROG    ::=     PROGRAMME  IDF  DEBUT LDECL:ld  LINST:li  FIN
                {:  Programme p = new Programme(ldleft + 1);
                    p.ajouterDeclarations(ld);
                    p.ajouterInstructions(li);
                    RESULT = p; :}
                ;

LDECL   ::=     LDECL:ld DECL_VAR:d
                {:  ((BlocDeDeclarations)ld).ajouter((Declaration)d);
                    RESULT = ld; :}
        |
                DECL_VAR:d
                {:  BlocDeDeclarations b = new BlocDeDeclarations(dleft + 1);
                    b.ajouter(d);
                    RESULT = b; :}
                ;

DECL_VAR    ::= ENTIER IDF:i POINTVIRGULE
                {: RESULT = new Declaration(new IDF(i, ileft + 1), ileft + 1); :}
                ;

LINST   ::=     LINST:li  INST:i
                {: ((BlocDInstructions)li).ajouter((Instruction)i) ;
                   RESULT = li ; :}
        |
                INST:i
                {:  BlocDInstructions b = new BlocDInstructions(ileft + 1);
                    b.ajouter(i);
                    RESULT = b ; :}
                ;

INST    ::=     ECR:e
                {: RESULT = e ; :}
        |
                AFF:a
                {: RESULT = a; :}
        |
                LECTURE:l
                {: RESULT = l; :}
                ;

AFF     ::=     IDF:i EGAL EXP:e POINTVIRGULE
                {: RESULT = new Affectation(new IDF(i, ileft + 1), e, ileft + 1); :}
                ;

ECR     ::=     ECRIRE EXP:e POINTVIRGULE
                {: RESULT = new Ecrire(e, eleft + 1) ; :}
                ;

LECTURE ::=     LIRE IDF:i POINTVIRGULE
                {: RESULT = new Lire(new IDF(i, ileft + 1), ileft + 1); :}
                ;
    
EXP     ::=     CSTENTIERE:c
                {: RESULT = new ConstanteEntiere(c, cleft + 1) ; :}
        |
                PAR_OUV EXP:e PAR_FER
                {: RESULT = e; :}
        |
                IDF:i
                {:  IDF idf = new IDF(i, ileft + 1);
                    RESULT = idf; :}
        |
                MOINS EXP:e
                {: RESULT = new Soustraction(new ConstanteEntiere("0", eleft + 1), e, eleft + 1); :}
        |
                EXP:e1 PLUS EXP:e2
                {: RESULT = new Addition(e1, e2, e1left + 1); :}
        |
                EXP:e1 MOINS EXP:e2
                {: RESULT = new Soustraction(e1, e2, e1left + 1); :}
        |
                EXP:e1 FOIS EXP:e2
                {: RESULT = new Multiplication(e1, e2, e1left + 1); :}
        |
                EXP:e1 DIV EXP:e2
                {: RESULT = new Division(e1, e2, e1left + 1); :}
                ;
